group 'com.nike'

buildscript {
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

// load environment specific props from global and environment profiles
def environment = project.hasProperty('env') ? project.'env' : 'default'
Properties props = new Properties()
def globalProperties = new File("$project.projectDir/profile/${environment}.properties")
if (globalProperties.exists()) {
    props.load(new FileInputStream(globalProperties))
}
props.load(new FileInputStream("$project.projectDir/profile/${environment}.properties"))
project.ext.set('environment', environment)
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: "com.fieldju.aws-sam-deployer"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = "0.7.6.201602180812"
        reportsDir = file("$buildDir/reports")
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
            html.destination "${buildDir}/reports/jacocoHtml"
            xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/com/nike/cerberus/lambda/waf/CloudFrontLogEvent*'
                ])
            })
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath

        // copy the needed props to the int tests
        systemProperties = [
                arn: System.getProperty('arn'),
                bucketName: System.getProperty('bucketName'),
                logKey: System.getProperty('logKey')
        ]
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    integrationTest {
        testLogging {
            showStandardStreams = true
        }
    }
}

