group 'com.nike.cpe'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0'
    }
}

apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/reports")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacocoHtml"
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/com/nike/cerberus/lambda/waf/CloudFrontLogEvent*'
            ])
        })
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

tasks.check.dependsOn jacocoTestReport

dependencies {
    compile group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.1.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-log4j', version: '1.0.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '1.3.0'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-cloudformation', version: '1.11.37'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.3'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-net', name: 'commons-net', version: '3.5'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-waf', version: '1.11.39'
    compile group: 'com.fieldju', name: 'slack-client', version: '2.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'

}

def artifactoryUser = project.hasProperty('artifactoryUser') ? project.artifactoryUser
        : System.getenv('ARTIFACTORY_USER') ?: ''

def artifactoryPassword = project.hasProperty('artifactoryPassword') ? project.artifactoryPassword
        : System.getenv('ARTIFACTORY_PASSWORD') ?: ''


artifactory {
    contextUrl = 'https://artifactory.nike.com/artifactory'
    publish {
        repository {
            if (version.contains('-SNAPSHOT')) {
                repoKey = 'blueprint-snapshots'
            } else {
                repoKey = 'blueprint-release'
            }
            username = artifactoryUser
            password = artifactoryPassword
            maven = true

        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = 'all-repos'
            username = artifactoryUser
            password = artifactoryPassword
            maven = true

        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
}

artifactoryPublish {
    dependsOn shadowJar
}


configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    // copy the needed props to the int tests
    systemProperties = [
            arn: System.getProperty('arn'),
            bucketName: System.getProperty('bucketName'),
            logKey: System.getProperty('logKey')
    ]
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    testLogging {
        events "passed", "skipped", "failed"
    }
}