import com.fieldju.gradle.plugins.lambdasam.tasks.MultiRegionPackageAndDeploySamTask

version '1.0.0'

shadowJar {
    baseName = 'cerberus-clean-up-lambda'
    classifier = null
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'

    compile group: 'com.amazonaws', name: 'aws-lambda-java-log4j', version: '1.0.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '1.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile 'com.nike:cerberus-client:2.3.0'
}

task deploy(type: MultiRegionPackageAndDeploySamTask) {
    regions = getProfileProperty('clean_up.regions').split(/,/)
    stackName = "${project.environment}-${project.name}"
    templatePath = "${project.projectDir}${File.separator}sam.yaml"
    regionToS3BucketMap = getRegionBucketMap(getProfileProperty('cerberus.lambda_uploads_region_buckets'))
    tokenArtifactMap = [
            '@@CODE_URI@@': "${project.buildDir}${File.separator}libs${File.separator}${project.getName()}-${version}.jar"
    ]
    parameterOverrides = [
            CerberusUrl: getProfileProperty('cerberus.url'),
            CerberusEnvironment: "${project.environment}",
            Region: getProfileProperty('clean_up.primary_region')
    ]
    logStackOutputs = true
}